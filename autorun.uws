//---------------------------------------------------------------------
// Time Clickers 自動実行スクリプト
// ( UWSC 5.1.1 Firefox 39.0 にて動作確認 )
//---------------------------------------------------------------------

Option Explicit

//---------------------------------------------------------------------
// 変数・定数宣言
//---------------------------------------------------------------------

Public TIME_YY          //年 4桁
Public TIME_MM          //月 01 - 12
Public TIME_DD          //日 01 - 31
Public TIME_HH          //時 00 - 23
Public TIME_NN          //分 00 - 59
Public TIME_SS          //秒 00 - 59
Public GETTIME_sec      //GETTIME()の戻り値

Public resetTime        //リセット実施(初回起動)時刻
Public resetInterval    //リセット実行間隔
Public nextCaptureTime  //次のスクリーンショット取得時刻
Public captureCount     //スクリーンショットカウンター

//スクリプト用iniファイルの存在チェック
if FOPEN(GET_CUR_DIR + "\" + "autorun.ini", F_EXISTS) = FALSE
  MSGBOX("設定ファイルが存在しません")
  EXITEXIT
endif

Const initResetInterval      = READINI("SETTING", "initResetInterval")       //リセット実行間隔初期値

//ブロッククリック開始座標
Const clickStartPosition_x = READINI("SETTING", "clickStartPosition_x")
Const clickStartPosition_y = READINI("SETTING", "clickStartPosition_y")
Const increment_x = READINI("SETTING", "increment_x")                        //X座標増分
Const increment_y = READINI("SETTING", "increment_y")                        //Y座標増分

//TIME WARPボタン(ACTIME ABILITIES UNLOCKボタン)の座標
Const timeWarpPosition_x = READINI("SETTING", "timeWarpPosition_x")
Const timeWarpPosition_y = READINI("SETTING", "timeWarpPosition_y")

// リセット実施YESボタン座標
Const timeWarpConfirmPositon_x = READINI("SETTING", "timeWarpConfirmPositon_x")
Const timeWarpConfirmPositon_y = READINI("SETTING", "timeWarpConfirmPositon_y")

// CLICK PISTOL BUYボタン座標
Const clickPistolPosiotn_x = READINI("SETTING", "clickPistolPosiotn_x")
Const clickPistolPosiotn_y = READINI("SETTING", "clickPistolPosiotn_y")

// Idle Mode ボタン座標
Const clickIdleModePosiotn_x = READINI("SETTING", "clickIdleModePosiotn_x")
Const clickIdleModePosiotn_y = READINI("SETTING", "clickIdleModePosiotn_y")

//---------------------------------------------------------------------
// 画面ID、ハンドルの取得
//---------------------------------------------------------------------
Dim windowId = GETID("Time Clickers")
Dim handle = GETCTLHND(windowId, "Unity.WebPlayer")

ifb windowId < 0
  MsgBox("対象のウインドウが起動していません")
  EXITEXIT
endif

ifb handle < 0
  MsgBox("Unity.WebPlayerがありません")
  EXITEXIT
endif

PRINT "一時停止：Alt+F1  停止：Alt+F2"


//---------------------------------------------------------------------
// クリック位置をクライアント座標(UnityWebPlayer内の座標)にする
//---------------------------------------------------------------------
MOUSEORG(handle, MORG_CLIENT)
//ウインドウをアクティブにする
CTRLWIN(windowId, ACTIVATE)

//---------------------------------------------------------------------
// 変数初期化
//---------------------------------------------------------------------
resetTime        = customizedGetTime()
nextCaptureTime  = resetTime
captureCount     = 0
resetInterval    = initResetInterval

//開始メッセージをログに出力
printDateAndMessage("start script")

//---------------------------------------------------------------------
// リセットしてからスクリプトを実行するか確認(デフォルト：リセットなし)
//---------------------------------------------------------------------
Dim initialResetFlag = SLCTBOX(SLCT_RDO, 10, "スクリプト実行前にリセットしますか？", "NO", "YES", "NO(定期リセットなし)")
//リセットなし
ifb initialResetFlag = SLCT_1
  customizedGetTime(initResetInterval/60/60/24)
  printMessageAndDate("auto reset →")
//リセットあり
elseif initialResetFlag = SLCT_2

  resetAndInitialProcess()

  printDateAndMessage("reset")

  customizedGetTime(resetInterval/60/60/24)
  printMessageAndDate("auto reset →")

  resetTime = customizedGetTime()
  nextCaptureTime  = resetTime
endif


//---------------------------------------------------------------------
// スレッド ※リセット効率のチェック時に使用する
//---------------------------------------------------------------------
ifb initialResetFlag = SLCT_2
  Dim captureFlag = SLCTBOX(SLCT_RDO, 10, "定期的にスクリーンショットを撮影しますか？", "NO(撮影しない)", "YES")

  SLEEP(1)

  ifb captureFlag = SLCT_2
    THREAD capture()
  endif
endif


//---------------------------------------------------------------------
// スクリプト本体
//---------------------------------------------------------------------
WHILE TRUE
  
  // Idle mode が無効になっていたらアクティブにする
  ifb PEEKCOLOR(clickIdleModePosiotn_x, clickIdleModePosiotn_y, COL_RGB) = 26879 OR PEEKCOLOR(clickIdleModePosiotn_x, clickIdleModePosiotn_y, COL_RGB) = 4165119
    BTN(LEFT,CLICK,clickIdleModePosiotn_x,clickIdleModePosiotn_y)
  endif 

  // Abilityをすべてアクティブにする
  KBD(VK_SPACE,CLICK)

  // Cooldownが使用可能になったら実行する
  ifb PEEKCOLOR(955, 355, COL_RGB) = 26623
    sleep(1)
    KBD(VK_0,CLICK)
  endif
  
  // Dimension Shiftをアクティブにする
  ifb PEEKCOLOR(815, 355, COL_RGB) = 26623
    KBD(VK_7,CLICK)
  endif

  //-----------------------------------------------------------------
  // ブロッククリック
  //-----------------------------------------------------------------
  clickAndMove()

  //----------------------------------------------------
  // アップグレード自動購入
  //----------------------------------------------------
  KBD(VK_A,CLICK)                 // PULSE PISTOL
  KBD(VK_S,CLICK)                 // FLAK CANNON
  KBD(VK_D,CLICK)                 // SOREAD RIFLE
  KBD(VK_F,CLICK)                 // POCKET LAUNCHER
  KBD(VK_G,CLICK)                 // PARTICLE BALL

  //----------------------------------------------------
  // リセット＋初期処理(放置してる場合は自動でリセット)
  //----------------------------------------------------
  //スクリプト起動(またはリセット)して一定時間後にリセット
  ifb (!(initialResetFlag = SLCT_3) AND customizedGetTime() - resetTime) >= resetInterval
    Dim resetConfirm = SLCTBOX(SLCT_RDO, 10, "リセットしますか？", "YES", "NO(10分後)", "NO(30分後)", "NO(60分後)")
    Dim resetMessage = "reset cancel"

    //タイムアウトかYESの場合リセット実施
    ifb resetConfirm = 0 OR resetConfirm = SLCT_1

      resetAndInitialProcess()

      resetMessage = "reset"

      //リセット間隔を初期化
      resetInterval = initResetInterval

      //スクリーンショット関連も初期化
      captureCount = 0
    elseif resetConfirm = SLCT_2
      // 10分後
      resetInterval = 600
    elseif resetConfirm = SLCT_3
      // 30分後
      resetInterval = 1800
    elseif resetConfirm = SLCT_4
      // 60分後
      resetInterval = 3600
    endif

    printDateAndMessage(resetMessage)

    //次のリセット時刻を取得
    customizedGetTime(resetInterval/60/60/24)
    printMessageAndDate("auto reset →")

    resetTime = customizedGetTime()
    nextCaptureTime = resetTime

  endif

WEND

//---------------------------------------------------------------------
// GETTIMEで取得できる値をグローバル変数に保持させる
//---------------------------------------------------------------------
FUNCTION customizedGetTime(adjustmentValue = EMPTY)

  ifb adjustmentValue = EMPTY
    RESULT = GETTIME()
  else
    RESULT = GETTIME(adjustmentValue)
  endif

  //念のためPublic変数に保持
  GETTIME_sec = RESULT

  TIME_YY = G_TIME_YY4
  TIME_MM = G_TIME_MM2
  TIME_DD = G_TIME_DD2
  TIME_HH = G_TIME_HH2
  TIME_NN = G_TIME_NN2
  TIME_SS = G_TIME_SS2

FEND

//---------------------------------------------------------------------
// 日付＋メッセージをログに出力
//---------------------------------------------------------------------
PROCEDURE printDateAndMessage(message)

  PRINT "" + TIME_YY + "/" + TIME_MM + "/" + TIME_DD + " " + TIME_HH + ":" + TIME_NN + ":" + TIME_SS + " " + message

FEND

//---------------------------------------------------------------------
// メッセージ＋日付をログに出力
//---------------------------------------------------------------------
PROCEDURE printMessageAndDate(message)

  PRINT message + " " + TIME_YY + "/" + TIME_MM + "/" + TIME_DD + " " + TIME_HH + ":" + TIME_NN + ":" + TIME_SS

FEND

//---------------------------------------------------------------------
// リセット実行と初期処理
//---------------------------------------------------------------------
PROCEDURE resetAndInitialProcess()

  Dim i, j

  //「TIME WARP」をクリック
  BTN(LEFT,CLICK,timeWarpPosition_x,timeWarpPosition_y,300)
  //「YES」をクリック
  BTN(LEFT,CLICK,timeWarpConfirmPositon_x,timeWarpConfirmPositon_y,300)

  sleep(2)

  //「PISTOL LEVEL UP」をクリック
  FOR i = 1 TO 13
    BTN(LEFT,CLICK,clickPistolPosiotn_x,clickPistolPosiotn_y,300)
  NEXT

  //「UNLOCK ACTIVE ABILITIES」をクリック
  FOR i = 1 TO 11
    BTN(LEFT,CLICK,timeWarpPosition_x,timeWarpPosition_y,300)
  NEXT

FEND

//---------------------------------------------------------------------
// 一定間隔でスクリーンショットを撮る(TimeCube数確認用)
//---------------------------------------------------------------------
PROCEDURE capture()

  Const saveDirectory   = GET_CUR_DIR + "\screenshot\"
  Const captureInterval = 300

  //保存先のフォルダが存在しない場合は終了
  if FOPEN(saveDirectory, F_EXISTS) = FALSE
    MSGBOX(saveDirectory + "が存在しません")
    EXITEXIT
  endif

  WHILE TRUE
    ifb customizedGetTime() >= nextCaptureTime

      // 年月日時分秒_目安の経過時間.bmp形式で保存
      Dim file_path = saveDirectory + TIME_YY + TIME_MM + TIME_DD + TIME_HH + TIME_NN + TIME_SS + "_" + (captureInterval * captureCount)
      captureCount = captureCount + 1

      SAVEIMG(file_path, GETID("Time Clickers"), , , , , , 0, 0)

      printDateAndMessage("captured")

      nextCaptureTime = nextCaptureTime + captureInterval

    endif
  WEND

FEND

//---------------------------------------------------------------------
// クリック処理
//---------------------------------------------------------------------
PROCEDURE clickAndMove()
  Dim i, j
  Dim coordinate_x, coordinate_y

  FOR i = 1 TO 10
    FOR j = 1 TO 10
      coordinate_x = clickStartPosition_x + increment_x * (i - 1)
      coordinate_y = clickStartPosition_y + increment_y * (j - 1)
      BTN(LEFT,CLICK,coordinate_x,coordinate_y)
    NEXT
  NEXT

FEND
